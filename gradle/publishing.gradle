allprojects {
	if(!project.hasProperty('bintrayUser'))
	{
		project.ext.bintrayUser = System.getenv('BINTRAY_USER')
	}
	if(!project.hasProperty('bintrayKey'))
	{
		project.ext.bintrayKey = System.getenv('BINTRAY_KEY')
	}

	if(System.getenv('JENKINS_BINTRAY_CREDENTIALS'))
	{
		def credentials = System.getenv('JENKINS_BINTRAY_CREDENTIALS').split(':')
		project.ext.bintrayUser = credentials[0]
		project.ext.bintrayKey = credentials[1]
	}

	apply plugin: 'com.jfrog.artifactory'
	apply plugin: 'com.jfrog.bintray'
	apply plugin: 'maven-publish'
	apply plugin: 'nebula-publishing'

	artifactory {
		contextUrl = 'https://oss.jfrog.org'
	}

	// Multi-project equivalent of nebula-maven-distribute
	publishing {
		repositories {
			maven {
				name 'distMaven'
				url rootProject.file('distMaven').toURI().toURL()
			}
		}
	}
	def distTask = tasks.findByName('distribute') ?: tasks.create(name: 'distribute')
	distTask.dependsOn 'publishMavenNebulaPublicationToDistMavenRepository'
	distTask.outputs.dir rootProject.file('distMaven')
	tasks.matching({ it.name == 'clean' }).all { cleanTask -> cleanTask.dependsOn('cleanDistribute')}

	bintray {
		user = project.bintrayUser
		key = project.bintrayKey

		publications = ['mavenNebula']
		publish = true

		pkg {
			repo = 'maven'
			name = 'vetinari'
			desc = 'Vetinari static site generator'
			websiteUrl = 'https://github.com/roguePanda/vetinari'
			issueTrackerUrl = 'https://github.com/roguePanda/vetinari/issues'
			vcsUrl = 'https://github.com/roguePanda/vetinari.git'
			licenses = ['Apache-2.0']
			labels = ['gradle', 'static-site']
		}
	}

	bintrayUpload.dependsOn publish
	artifactoryPublish.dependsOn publish

	afterEvaluate {
		publishing.publications.withType(MavenPublication) { pub ->
			pom.withXml {
				asNode().dependencies.'*'.findAll() { depNode ->
					def groupId = depNode.groupId.text()
					def artifactId = depNode.artifactId.text()
					def configuration = project.configurations.find {
						it.dependencies.any { // not allDependencies because that looks at extended configurations
							it.group == groupId && it.name == artifactId
						}
					}

					def dependency = project.configurations.collect { it.resolvedConfiguration.resolvedArtifacts }.flatten().find {
						def id = it.moduleVersion.id
						id.group == groupId && id.name == artifactId
					}

					def mavenScope = [runtime: 'runtime',
						compile: 'compile',
						testCompile: 'test',
						testRuntime: 'test',
						provided: 'provided'].get(configuration.name)
					depNode.scope*.value = mavenScope
					depNode.version*.value = dependency.moduleVersion.id.version
				}
			}
		}
	}
}

artifactoryPublish.skip = true

// It seems that adding signing.* properties enables signing, which causes an error since the both the main jar file and
// the signature (.asc) for the jar file have null classifiers, so the maven-publish plugin can't tell which is the main artifact

artifactory {
	contextUrl = 'https://oss.jfrog.org'
	resolve {
		repository {
			repoKey = 'libs-release'
		}
	}
	publish {
		repository {
			repoKey = 'oss-snapshot-local'
			username = project.bintrayUser
			password = project.bintrayKey
			mavenCompatible = true
		}
		defaults {
			publications 'mavenNebula'
		}
	}
}

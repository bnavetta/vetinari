import java.time.Year

buildscript {
	repositories {
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:latest.release'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:latest.release'
		classpath 'com.netflix.nebula:nebula-project-plugin:latest.release'
	}
}

allprojects {
	apply plugin: 'eclipse'
	apply plugin: 'idea'

	group = 'com.bennavetta.vetinari'
	version = '0.1-beta'
}

subprojects {
	apply plugin: 'nebula-project'
	apply plugin: 'license'
	apply plugin: 'groovy'
	apply plugin: 'com.jfrog.bintray'

	// It seems that adding signing.* properties enables signing, which causes an error since the both the main jar file and
	// the signature (.asc) for the jar file have null classifiers, so the maven-publish plugin can't tell which is the main artifact

	contacts {
		'ben.navetta@gmail.com' {
			moniker 'Ben Navetta'
			github 'roguePanda'
		}
	}

	license {
		header rootProject.file('gradle/header.txt')
		ext.year = Year.now().value
		skipExistingHeaders true
	}

	bintray {
		user = bintrayUser
		key = bintrayKey

		publications = ['mavenNebula']
		publish = true

		pkg {
			repo = 'vetinari'
			name = project.name
			licenses = ['Apache-2.0']
			labels = ['static-site']
			publicDownloadNumbers = true
		}
	}

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	repositories {
		jcenter()
		mavenCentral()
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.0'
}

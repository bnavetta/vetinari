configurations {
	provided
	compile.extendsFrom provided
}

description 'Core implementation of Vetinari'

dependencies {
	compile 'com.google.inject:guice:4.0-beta4+'
	compile 'com.google.inject.extensions:guice-multibindings:4.0-beta4+'

	compile 'com.typesafe:config:1.2.1+'

	// Use the Groovy version built with, since that's what Gradle is using
	compile "org.codehaus.groovy:groovy-all:${GroovySystem.version}"

	provided 'org.projectlombok:lombok:latest.release'
}

task delombok(type: JavaExec) {
	classpath sourceSets.main.compileClasspath
	main = 'lombok.core.Main'
	args 'delombok', 'src/main/java', '-d', "$buildDir/delombok"

	inputs.files sourceSets.main.allJava
	outputs.dir "$buildDir/delombok"
}

javadoc.dependsOn delombok
javadoc.source = "$buildDir/delombok"

sonarRunner {
	sonarProperties {
//		property "sonar.sources", "$buildDir/delombok"
// Delombok changes the formatting sometimes, and it uses a different style than I do
	}
}
rootProject.tasks.sonarRunner.dependsOn delombok